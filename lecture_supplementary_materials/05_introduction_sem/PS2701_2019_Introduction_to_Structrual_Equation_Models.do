PS2701
Fall 2019

Week 4:  INTRODUCTION TO STRUCTURAL EQUATION MODELING

EXAMPLE:  AMERICAN NATIONAL ELECTION STUDY PANEL, 2000-2002-2004

use "nes3wave.dta"


corr pid2000 pid2002 pid2004									// GENERATES CORRELATION MATRIX OF OBSERVED VARIABLES
corr pid2000 pid2002 pid2004, cov								// GENERATES COVARIANCE MATRIX OF OBSERVED VARIABLES
corr pid2004 pid2002 pid2000, cov								// PUTS LATER WAVES FIRST IN LINE WITH STANDARD SEM PROCEDURE

******DIAGRAM 1

sem (pid2004<-pid2002) (pid2002<-pid2000), standardized			// SEM COMMAND FOR ESTIMATION OF MODEL, STANDARDIZED RESULTS OPTION

estat framework, standardized fitted							// PUTS RESULTS IN TYPICAL SEM FRAMEWORK AND COMPARES OBSERVED AND "FITTED" (PREDICTED) VALUES FOR MEANS, VARIANCES, COVARIANCES
estat framework, fitted

estat mindices													// HOW WILL MODEL FIT DIFFER IF CONSTRAINTS ARE RELAXED?  

sem (pid2004<-pid2002) (pid2002<-pid2000)
estat gof, sta(all)    											//GOODNESS OF FIT STATISTICS -- MODEL AS A WHOLE
estat eqgof														//GOODNESS OF FIT STATISTICS -- EACH ENDOGENOUS EQUATION

****SATURATING MODELS

sem (pid2004<-pid2002 pid2000) (pid2002<-pid2000), standardized    // ONE SATURATED MODEL RESULTING FROM RELAXATION OF CONSTRAINT 
                                                                   // THAT STRUCTURAL EFFECT FROM PID2000->PID2004 IS ZERO
sem (pid2004<-pid2002) (pid2002<-pid2000),cov(e.pid2004*e.pid2002) // ANOTHER SATURATED MODEL (POSSIBLY IMPLAUSIBLE BUT 
                                                                   // ILLUSTRATIVE THAT MANY MODELS CAN FIT THE SAME DATA!!!)

*** CONSTRAINING EFFECTS AND TESTING ALTERNATIVE MODELS

sem (pid2004<-pid2002) (pid2002<-pid2000)
sem (pid2004<-pid2002@a) (pid2002<-pid2000@a)					//  EXAMPLE OF CONSTRAINING ONE COEFFICIENT TO EQUAL ANOTHER; 
                                                                //  CAN THEN COMPARE MODEL FIT FOR "FULL" AND "REDUCED" MODELS

																
****HAVE STATA CALCULATE THE SIGNIFICANCE OF THE DIFFERENCE IN CHI-SQUARES OF NESTED MODELS
dis chi2tail(1, 124.40-117.94) 		
														
