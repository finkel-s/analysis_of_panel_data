PS2701 LONGITUDINAL ANALYSIS

WEEKS 10:  MIXED MODELS/HIERARCHICAL GROWTH MODELS

*USE DEMOCRACY.DATA
xtset cc_2 yearnum
tab yearnum, gen(yrr)         //GENERATES YEAR DUMMIES
quietly tab cc_2, gen(count)  //GENERATES COUNTRY DUMMIES

gen yearnum2=yearnum-1       // RECODES YEARNUM SO THAT THERE IS A TRUE ZERO POINT AT 1990

**THE RANDOM INTERCEPT MODEL AS A KIND OF 'MIXED' MODEL

xtreg dg01i , re theta  //RECALL THE 'GLS' ESTIMATION OF THE RANDOM EFFECTS MODEL

**RANDOM EFFECTS 
mixed dg01i || cc_2:     //THIS IS A MAXIMUM LIKELIHOOD ESTIMATION OF THE SAME RANDOM EFFECTS MODEL
                        //WHY 'MIXED'?  EVERY CASE HAS THE SAME 'FIXED' COMPONENT, THE OVERALL POPULATION MEAN, 
                        //WHICH IS THEN AUGMENTED BY A 'RANDOM' COMPONENT.  SO IT IS A 'MIXED' MODEL OF FIXED AND RANDOM EFFECTS
                        //IT COULD ALSO BE CALLED AN 'UNCONDITIONAL MEANS' MODEL, BECAUSE THE ONLY THING IN THE MODEL IS A DIFFERENT INTERCEPT
                        //FOR EACH COUNTRY, OR A DIFFERENT MEAN LEVEL OF DEMOCRACY FOR ALL TIME POINTS.  NO OTHER INDEPENDENT VARIABLES AND NO TIME EFFECTS

***THE INTERCLASS CORRELATION:  HOW MUCH OF TOTAL VARIATION IS DUE TO UNIT-LEVEL (BETWEEN SUBJECT) VARIATION, AND HOW MUCH DUE TO WITHIN SUBJECT VARIATION
//                              OVER TIME?  THIS IS 'RHO' IN XTREG, ALSO REFERRED TO AS THE 'ICC' OR INTERCLASS CORRELATION

display (37.66)/(37.66+6.88)  // = .846, or 84.6% OF TOTAL VARIATION IN POLITY SCORES IS BETWEEN COUNTRIES.  NOTE SAME VALUE AS RHO IN XTREG


**'MANUAL'GROWTH TRAJECTORY ANALYSIS

**SHOW SOME GROWTH TRAJECTORIES
xtline dg01i if cc_2==3062009 |cc_2==1011013 |cc_2==2005012 |cc_2==2013005 //GROWTH TRAJECTORIES FOR PAKISTAN, NIGERIA
                                                                           //VENEZUELA, MEXICO

//NOTE HOW COUNTRIES START AT DIFFERENT POINTS IN 1990 AND HAVE DIFFERENT LEVEL 1 RATES OF CHANGE, I.E. LOOKS LIKE
//SOME HETEROGENEITY TO EXPLAIN!!

xtline dg01i if cc_2==3062009 |cc_2==1011013 |cc_2==2005012 |cc_2==2013005, overlay

***START WITH THE OLS POOLED ESTIMATE OF DEMOCRATIC GROWTH 
reg dg01i yearnum2  //THE POPULATION INTERCEPT IS .57 AND THE POPULATION SLOPE FOR 'TIME' IS .20

***SHOW THE HETEROGENEITY IN THESE PARAMETERS
**WHAT WOULD 140 DIFFERENT OLS MODELS OF POLITY SCORES REGRESSED AGAINST TIME LOOK LIKE?
sort cc_2
regress dg01i yearnum2 if cc_2==1011001  //BENIN HAS INTERCEPT OF 4.45 AND SLOPE OF .17 IN OLS

statsby olsinter=_b[_cons] olsslope=_b[yearnum2], by(cc_2) clear: regress dg01i yearnum2  
//THIS RAN 140 REGRESSIONS AND STORES THE RESULTS IN VARIABLES CALLED 'INTER' AND 'SLOPE'
summarize olsinter olsslope
browse // SEE THE OLS ESTIMATES FOR INTERCEPT AND SLOPE FOR EACH COUNTRIES
histogram olsinter
histogram olsslope  //NOTE VARIATION IN BOTH PARAMETERS

**GET DEMOCRACY.DTA BACK:  IT HAS THE INTERCEPT AND SLOPE IN THE DATA SET, CALLED 'OLSINTER' AND 'OLSSLOPE1'
xtset cc_2 yearnum
tab yearnum, gen(yrr)         //GENERATES YEAR DUMMIES
quietly tab cc_2, gen(count)  //GENERATES COUNTRY DUMMIES
gen yearnum2=yearnum-1       // RECODES YEARNUM SO THAT THERE IS A TRUE ZERO POINT AT 1990


**CAN WE PREDICT THE INTERCEPT AND THE SLOPE WITH INDEPENDENT VARIABLES? 
reg olsinter l225 l203 //DOES INTERCEPT DEPEND ON AMOUNT OF ETHNIC FRACTIONALIZATION AND YEARS OF PRIOR DEMOCRACY?
                    //THINK ABOUT THESE PARAMETERS:  THE SLOPE, THE INTERCEPT, THE R-SQUARED, AND THE RESIDUAL VARIATION
predict prdolsinter, xb  //THE PREDICTED INTERCEPT 
predict prdinterresid, resid   //THE ESTIMATED RESIDUAL IN THE PREDICTION OF THE INTERCEPT
summarize prdinterresid  //SO THERE IS A GOOD DEAL OF UNEXPLAINED VARIATION IN THE OLS INTERCEPTS
corr olsinter prdolsinter  // SHOWS CORRELATION OF .65 BETWEEN PREDICTED AND ACTUAL OLS INTERCEPTS

// DO SAME FOR THE OLS SLOPE

reg olsslope l225 l203 //DOES SLOPE DEPEND ON AMOUNT OF ETHNIC FRACTIONALIZATION AND YEARS OF PRIOR DEMOCRACY?
                    //THINK ABOUT THESE PARAMETERS:  THE SLOPE, THE INTERCEPT, THE R-SQUARED, AND THE RESIDUAL VARIATION
predict prdolsslope, xb  //THE PREDICTED SLOPE
predict predsloperesid, resid
summarize predsloperesid    // SO GOOD DEAL OF UNEXPLAINED VARIATION IN THE OLS SLOPES TOO
corr olsslope prdolsslope  //  CORRELATION OF ONLY .35 BETWEEN PREDICTED AND ACTUAL OLS SLOPES

//CONCLUSION:  WE CAN EXPLAIN SOME, BUT NOT ALL OF VARIATION IN INTERCEPTS AND SLOPES WITH 2 INDEPENDENT VARIABLES
// AND STILL MUCH VARIATION OF INDIVIDUAL CASES AROUND THE PREDICTED VALUES FROM THE GROWTH TRAJECTORIES


//GROWTH MODELING IS EXTENSTION OF THESE BASIC IDEAS, FORMALIZES IN MORE PARSIMONIOUS RANDOM EFFECTS MODEL 
//THE AMOUNT OF VARIATION IN INTERCEPTS AND SLOPES (I.E., THE GROWTH TRAJECTORIES) THAT EXIST, 
//AS WELL AS THE AMOUNT OF VARIATION IN OUTCOMES OF PARTICULAR UNITS AROUND THEIR PREDICTED TRAJECTORIES.  
//THEN USE INDEPENDENT VARIABLES TO TRY TO ACCOUNT FOR INTERCEPT, SLOPE VARIATION AND USE THE ENTIRE MODEL 
//TO ACCOUNT FOR OVERALL VARIATION OF THE INDIVIDUAL Y VALUES AT EACH POINT IN TIME

**START WITH BASIC RANDOM EFFECTS MODEL, NO TIME TREND AT ALL
** ALTERNATIVE MIXED MODELS FOR RANDOM INTERCEPT EFFECTS

mixed dg01i || cc_2 :                                  //MODEL 1:  THE UNCONDITIONAL MEANS MODEL ("UM" FROM THE POWERPOINT)
mixed dg01i l203 l221 l212 l225 || cc_2:               //MODEL 2:  INCLUDES LEVEL 2 PREDICTORS OF INTERCEPT ("A" FROM THE POWERPOINT)

***CALCULATING R-SQUARED IN MIXED MODELS

***1)  HOW MUCH HAVE NEW IVS REDUCED THE LEVEL 1 RESIDUAL VARIATION?  COMPARE LEVEL 1 RESIDUAL VARIATION FROM MODEL 2 TO MODEL 1
display (6.88-6.88)/6.88 // = .00  //THIS IS NORMAL -- WE DID NOT ADD ANYTHING AT LEVEL 1, SO LEVEL 1 RESIDUAL IS THE SAME.  
						//ADDING TIME-VARYING COVARIATES AT LEVEL 1 (SUCH AS INFLATION OR AID DEMOCRACY EXPENDITURES) WOULD CHANGE THIS

***2)  HOW MUCH HAVE NEW IVS REDUCED THE VARIATION IN THE RANDOM INTERCEPT?  COMPARE UNIT-LEVEL INTERCEPT VARIATION 
     //FROM MODEL 2 TO MODEL 1
display (37.67 - 23.85)/37.67 // = .37, OR 37% OF RANDOM INTERCEPT VARIATION EXPLAINED BY IVS.  

// SO WE HAVE SEVERAL KINDS OF R-SQUAREDS:  EXPLAINING VARIANCE IN THE OVERALL LEVEL 1 UNIQUE RESIDUALS AND THE LEVEL 1 RANDOM EFFECTS


**INCORPORATING THE TIME TREND INTO MIXED MODELS
xtset cc_2 yearnum

**THE 'UNCONDITIONAL' RANDOM SLOPE OR 'UNCONDITIONAL GROWTH' MODEL
mixed dg01i yearnum2 || cc_2:  yearnum2 //NOTE THE FIXED EFFECTS ARE THE SAME AS THE OLS ESTIMATES WE STARTED WITH 
                                        //NOTE ALSO THE LACK OF CORRELATION SPECIFIED BETWEEN THE TWO RANDOM EFFECTS (BY ASSUMPTION)
mixed dg01i yearnum2 || cc_2:  yearnum2, cov(unstructured)  // THE FULL UNCONDITIONAL GROWTH MODEL: INCLUDES THE ESTIMATED CORRELATION 
                                //BETWEEN INTERCEPT AND RATE OF CHANGE, SUGGESTS THAT COUNTRIES STARTING LOW(HIGH) INCREASE(DECREASE) MORE QUICKLY OVER TIME.

**R-SQUARED:  HOW MUCH OF THE CONDITIONAL LEVEL 1 RESIDUAL VARIATION DOES EACH COUNTRY'S LINEAR SLOPE EXPLAIN?
**COMPARE LEVEL 1 RESIDUAL VARIATION IN THE UNCONDITIONAL MEANS MODEL TO THIS MODEL
display (6.88-3.94)/6.88 // = .43, OR 43% OF OVERALL LEVEL 1 VARIATION ACCOUNTED FOR ALLOWING EACH COUNTRY TO HAVE A LINEAR SLOPE

***NOW, WHAT CAUSES THE VARIATION IN THE SLOPE?  MODEL B CONTAINS LEVEL 2 PREDICTORS OF BOTH INTERCEPTS AND SLOPES AT LEVEL 1

gen yrdemoc=yearnum2*l203  //CREATE NEW VARIABLES THAT INTERACT WITH TIME TO ENTER INTO XTMIXED
gen yrethnic=yearnum2*l225
mixed dg01i l203 l221 l225 l212 yearnum2 yrdemoc yrethnic || cc_2:  yearnum2 , cov (unstructured) //MODEL B FROM CLASS POWERPOINT


**R-SQUARED VALUES: 
***1)  REDUCTION IN LEVEL 1 VARIATION:  COMPARE LEVEL 1 RESIDUAL VARIATION FROM UNCONDITIONAL GROWTH MODEL TO MODEL B
display (3.95-3.94)/3.95 // = .002, SO AGAIN NO CHANGE 
***2)  HOW MUCH HAVE NEW IVS REDUCED THE VARIATION IN THE RANDOM INTERCEPT?  COMPARE UNIT-LEVEL INTERCEPT VARIATION FROM UNCONDITIONAL GROWTH MODEL TO MODEL B
display (44.54 -25.11)/44.54 // = .44, OR 44% OF RANDOM INTERCEPT VARIATION EXPLAINED BY IVS.  
***3) HOW MUCH HAVE NEW IVS REDUCED THE VARIATION IN THE RANDOM SLOPE?  COMPARE RANDOM SLOPE VARIATION
display (.128-.110)/.128 // = .14, OR 14% OF RANDOM SLOPE VARIATION EXPLAINED BY IVS

**COMPARING THE FIT OF DIFFERENT MODELS USING DEVIANCE AND INFORMATION CRITERIA

**1)  IS THE UNCONDITIONAL GROWTH MODEL 'BETTER' THAN THE UNCONDITIONAL MEANS MODEL?
mixed dg01i || cc_2: , mle  //THIS GIVES THE FULL INFORMATION ML ESTIMATES, NEEDED BECAUSE THE 
                              //FIXED EFFECTS ARE NOT THE SAME FOR THE TWO MODELS

mixed dg01i yearnum2 || cc_2: yearnum2, mle cov(unstructured)

**MEANS MODEL IS NESTED WITHIN THE GROWTH MODEL: CONSTRAINT THAT B(YEARNUM)=0, VARIANCE(YEARNUM)=0, 
*                                                AND COVARIANCE OF INTERCEPT AND SLOPE RANDOM EFFECTS=0

**DEVIANCE, UNCONDITIONAL MEANS MODEL 
display -2*(-4975.9) // = 9951.8
**DEVIANCE, UNCONDITIONAL GROWTH MODEL
display -2*(-4616.5) // =  9233.0

**DIFFERENCE = (DEVIANCE OF CONSTRAINED MODEL - DEVIANCE OF UNCONSTRAINED MODEL)
display 9951.8-9233.0  // = 718.8 WITH 3 DEGREES OF FREEDOM , FAR EXCEEDS CRITICAL VALUE. SO UNCONSTRAINED MODEL IS 'BETTER'


**2)  IS THE MODEL B 'BETTER' THAN THE UNCONDITIONAL GROWTH MODEL?

**UNCONDITIONAL GROWTH IS NESTED WITHIN MODEL B (6 DIFFERENT CONSTRAINTS ON FIXED EFFECTS)
**DEVIANCE, MODEL B
mixed dg01i l203 l221 l225 l212 yearnum2 yrdemoc yrethnic  || cc_2:  yearnum2 , cov (unstructured) mle
display -2*(-4574.4) // = 9148.8
display 9233.0-9148.8 // =  84.2 WITH 6 DEGREES OF FREEDOM, SO UNCONSTRAINED MODEL IS 'BETTER'

**CAN ALSO USE 'AIC' INFORMATION CRITERIA TO COMPARE ALL MODELS, NESTED OR NOT.  TAKES DEVIANCE AND ADDS 'PENALTY' FOR
**NUMBER OF PARAMETERS ESTIMATED IN THE MODEL.  

**AIC, UNCONDITIONAL MEANS:  3 PARAMETERS TO ESTIMATE (1 FIXED EFFECT, 2 VARIANCE COMPONENTS)
display -2*(-4975.9 - 3) // = 9957.8
**AIC, UNCONDITIONAL GROWTH:  6 PARAMETERS TO ESTIMATE (2 FIXED EFFECTS, 4 VARIANCE COMPONENTS)
display -2*(-4616.5 - 6) // = 9245.0, SO DIFFERENCE BETWEEN THE TWO MODELS IS VERY LARGE, OVER 700
**AIC, MODEL B:  12 PARAMETERS TO ESTIMATE (8 FIXED EFFECTS, 4 VARIANCE COMPONENTS)
display -2*(-4574.4 - 12) // = 9172.8, SO DIFFERENCE BETWEEN MODELS IS ALSO LARGE, ABOUT 75

**BOTH METHODS SUGGESTS THAT MODEL B IS THE 'BEST' OF THE MODELS SO FAR


*****EFFECTS OF CENTERING
******mixed dg01i l203 l225 yearnum2 yrdemoc yrethnic  || cc_2:  yearnum2 , cov (unstructured) mle

*DOWNLOAD BEN JANN'S CENTERING ROUTINE : NET SEARCH CENTER, AND YOU WILL FIND IT
center l203
center l225
gen yrdemoccen=yearnum2*c_l203
gen yrethcen=yearnum2*c_l225
mixed dg01i c_l203 c_l225 yearnum2 yrdemoccen yrethcen  || cc_2:  yearnum2 , cov (unstructured) mle
mixed dg01i l203  l225   yearnum2 yrdemoc yrethnic || cc_2:  yearnum2 , cov (unstructured) //MODEL B FROM CLASS POWERPOINT

//// AS CAN SEE, THE MAIN DIFFERENCES ARE IN THE CONSTANT AND THE EFFECT OF YEARNUM2 (SINCE IT IS THE EFFECT OF TIME WHEN 
//// ALL THE OTHER VARIABLES ARE ZERO, WHICH IN THIS CASE MEANS THE 'AVERAGE COUNTRY' ON PRIOR DEMOC AND ETHNIC FRACTIONALIZATION,
//// NOT, AS BEFORE, FOR WHEN THOSE VARIABLES ARE ZERO


***'EMPIRICAL BAYES' OR MODEL-BASED PREDICTIONS OF THE RANDOM EFFECT.  EXAMPLE:  THE UNCONDITIONAL MEANS MODEL
mixed dg01i || cc_2:

predict ebrandint, reffect  // 'EMPIRICAL BAYES RANDOM INTERCEPT' PREDICTION

//      EMPIRICAL BAYES PREDICTIONS COME FROM A WEIGHTED AVERAGE OF THE EFFECT THAT WOULD BE OBTAINED THROUGH 
//	    A DUMMY VARIABLE (OLS) PROCEDURE AND THE POPULATION AVERAGE RANDOM EFFECT (WHICH IS ZERO).  
//	    SO THE 'EB' PREDICTION 'SHRINKS' THE DUMMY VARIABLE EFFECT BACK TO ZERO, DEPENDING ON 
//      A) HOW MUCH OVERALL RANDOM VARIANCE IN THE INTERCEPT THERE IS; 
//	    B) HOW MUCH OVERALL LEVEL 1 RESIDUAL VARIANCE THERE IS; AND 
//      C) HOW LARGE T IS
//      SO WE 'SHRINK' TO ZERO WHEN THERE IS MORE UNCERTAINTY ABOUT WHETHER THE OBSERVED LSDV DUMMY INTERCEPT VARIATION 
//      IS ACCURATE OR "RELIABLE."

//      WHEN THERE IS VERY LITTLE INTERCEPT VARIATION TO BEGIN WITH, WHEN THERE IS A LOT OF LEVEL 1 RESIDUAL VARIATION AROUND THE INTERCEPT, AND WHEN THERE 
//      ARE A SMALL NUMBER OF OBSERVATION OCCASIONS, WE ARE MORE UNCERTAIN.  WHEN THERE IS A LOT OF INTERCEPT VARIATION, SMALL LEVEL 1 VARIATION AROUND THE
//      INTERCEPT AND A LARGE T, WE KEEP THE DUMMY (LSDV/FE-TYPE) ESTIMATES

//      FORMULA:  EB=FE ESTIMATE*SHRINKAGE FACTOR 
//      [RANDOM INTERCEPT VARIANCE/(RANDOM INTERCEPT VARIANCE+(LEVEL 1 RESIDUAL VARIANCE/T)]

**SHOW THIS:

mixed dg01i || cc_2:
predict pred, xb
generate res = dg01i-pred
egen avresid=mean(res), by (cc_2)    // THIS CREATES THE FIXED EFFECT FOR THE INTERCEPT, AS IF WE DID A DUMMY VAR REGRESSION
list avresid if year==2000

*CALCULATE "SHRINKAGE" FACTOR
display 37.67/(37.67+(6.88/14)) // = .987.  SO WE ARE GOING TO USE ALMOST 99% OF THE DUMMIES FOR OUR EB ESTIMATES BECAUSE MOST OF VARIATION IS
                                       //BETWEEN VARIATION, LITTLE LEVEL 1 VARIATION, AND LARGISH T OF 14
                                      //[NOTE:  MORE SHRINKAGE MEANS THAT RANDOM EFFECTS ESTIMATES WILL BE DIFFERENT FROM THE FIXED EFFECT OR DUMMY MODELS]


generate eb2 = avresid*.987   //TAKES THE DUMMY VARIABLE ESTIMATE AND MULTIPLES BY THE SHRINKAGE FACTOR

list cc_2 avresid ebrandint eb2 if year==2000  //SHOWS THE SIMILARITY IN THE EMPIRICAL BAYES ESTIMATES



***EXTENSIONS

** Modeling heteroskedasticity and autocorrelation by time
mixed dg01i l203 l221 l225 l212 yearnum2 yrdemoc yrethnic || cc_2:  yearnum2 , cov (unstructured) //MODEL B FROM CLASS POWERPOINT
mixed dg01i l203 l221 l225 l212 yearnum2 yrdemoc yrethnic || cc_2:  yearnum2 , res(independent, by(yearnum2)) cov (unstructured) // HETEROSKEDASTICITY
mixed dg01i l203 l221 l225 l212 yearnum2 yrdemoc yrethnic || cc_2:  yearnum2 , res(ar1, t(yearnum2)) cov (unstructured) // AR1


***MORE ON CENTERING:  CENTERING TIME

center yearnum2
gen yrdemcen2=c_yearnum2*c_l203
gen yrethcen2=c_yearnum2*c_l225
mixed dg01i l203  l225   c_yearnum2 yrdemcen2 yrethcen2 || cc_2:  yearnum2 , cov (unstructured)
